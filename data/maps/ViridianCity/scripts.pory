const LOCALID_OLD_MAN = 3
const LOCALID_TUTORIAL_MAN = 4
const LOCALID_WOMAN = 5

mapscripts ViridianCity_MapScripts {
	MAP_SCRIPT_ON_TRANSITION: ViridianCity_OnTransition
}

script ViridianCity_OnTransition {
	setflag(FLAG_VISITED_VIRIDIAN_CITY)
	call_if_eq(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 0, ViridianCity_EventScript_SetOldManBlockingRoad)
	call_if_eq(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 1, ViridianCity_EventScript_SetOldManStandingByRoad)
	call_if_ge(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 2, ViridianCity_EventScript_SetOldManNormal)
	call_if_eq(VAR_MAP_SCENE_VIRIDIAN_CITY_GYM_DOOR, 0, ViridianCity_EventScript_TryUnlockGym)
	end
}

script ViridianCity_EventScript_SetOldManNormal {
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_FRLG_OLD_MAN_1)
	return
}

script ViridianCity_EventScript_SetOldManStandingByRoad {
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_FRLG_OLD_MAN_1)
	setobjectxyperm(LOCALID_TUTORIAL_MAN, 21, 8)
	setobjectmovementtype(LOCALID_TUTORIAL_MAN, MOVEMENT_TYPE_LOOK_AROUND)
	return
}

script ViridianCity_EventScript_SetOldManBlockingRoad {
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_FRLG_OLD_MAN_LYING_DOWN)
	setobjectxyperm(LOCALID_TUTORIAL_MAN, 21, 11)
	setobjectmovementtype(LOCALID_TUTORIAL_MAN, MOVEMENT_TYPE_FACE_DOWN)
	return
}

script ViridianCity_EventScript_TryUnlockGym {
	goto_if_unset(FLAG_KANTO_BADGE02_GET, Common_EventScript_NopReturn)
	goto_if_unset(FLAG_KANTO_BADGE03_GET, Common_EventScript_NopReturn)
	goto_if_unset(FLAG_KANTO_BADGE04_GET, Common_EventScript_NopReturn)
	goto_if_unset(FLAG_KANTO_BADGE05_GET, Common_EventScript_NopReturn)
	goto_if_unset(FLAG_KANTO_BADGE06_GET, Common_EventScript_NopReturn)
	goto_if_unset(FLAG_KANTO_BADGE07_GET, Common_EventScript_NopReturn)
	setvar(VAR_MAP_SCENE_VIRIDIAN_CITY_GYM_DOOR, 1)
	return
}

script ViridianCity_EventScript_GymDoorLocked {
	lockall
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterUp)
	waitmovement(0)
	delay(20)
	msgbox(ViridianCity_Text_GymDoorsAreLocked)
	closemessage
	applymovement(OBJ_EVENT_ID_PLAYER, ViridianCity_Movement_JumpDownLedge)
	waitmovement(0)
	releaseall
	end
}

script ViridianCity_Movement_JumpDownLedge {
	jump_2_down
	step_end
}

script ViridianCity_EventScript_CitySign {
	msgbox(ViridianCity_Text_CitySign, MSGBOX_SIGN)
	end
}

script ViridianCity_EventScript_TrainerTips1 {
	msgbox(ViridianCity_Text_CatchMonsForEasierBattles, MSGBOX_SIGN)
	end
}

script ViridianCity_EventScript_TrainerTips2 {
	msgbox(ViridianCity_Text_MovesLimitedByPP, MSGBOX_SIGN)
	end
}

script ViridianCity_EventScript_GymSign {
	msgbox(ViridianCity_Text_GymSign, MSGBOX_SIGN)
	end
}

script ViridianCity_EventScript_GymDoor {
	msgbox(ViridianCity_Text_GymDoorsAreLocked, MSGBOX_SIGN)
	end
}

script ViridianCity_EventScript_Boy {
	msgbox(ViridianCity_Text_CanCarryMonsAnywhere, MSGBOX_NPC)
	end
}

// Other old man, not the tutorial old man
script ViridianCity_EventScript_OldMan {
	lock
	faceplayer
	goto_if_eq(VAR_MAP_SCENE_VIRIDIAN_CITY_GYM_DOOR, 1, ViridianCity_EventScript_OldManGymLeaderReturned)
	msgbox(ViridianCity_Text_GymClosedWonderWhoLeaderIs)
	closemessage
	applymovement(LOCALID_OLD_MAN, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
	end
}

script ViridianCity_EventScript_OldManGymLeaderReturned {
	msgbox(ViridianCity_Text_ViridiansGymLeaderReturned)
	release
	end
}

script ViridianCity_EventScript_TutorialOldMan {
	lock
	faceplayer
	goto_if_set(FLAG_KANTO_BADGE01_GET, ViridianCity_EventScript_AskIfTeachyTVHelpful)
	goto_if_ge(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 2, ViridianCity_EventScript_TutorialCompleted)
	goto_if_eq(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 1, ViridianCity_EventScript_TutorialStart)
	goto_if_eq(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 0, ViridianCity_EventScript_TutorialNotReady)
	end
}

script ViridianCity_EventScript_AskIfTeachyTVHelpful {
	msgbox(ViridianCity_Text_HowsTeachyTVHelping, MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, NO, ViridianCity_EventScript_TeachyTVNotHelpful)
	msgbox(ViridianCity_Text_MyGrandsonOnTheShow)
	release
	end
}

script ViridianCity_EventScript_TeachyTVNotHelpful {
	msgbox(ViridianCity_Text_TooBusyForTeachyTV)
	release
	end
}

script ViridianCity_EventScript_TutorialCompleted {
	msgbox(ViridianCity_Text_WeakenMonsFirstToCatch)
	release
	end
}

script ViridianCity_EventScript_TutorialStart {
	call(ViridianCity_EventScript_DoTutorialBattle)
	release
	end
}

script ViridianCity_EventScript_TutorialNotReady {
	msgbox(ViridianCity_Text_ThisIsPrivateProperty)
	closemessage
	release
	end
}

script ViridianCity_EventScript_Youngster {
	lock
	faceplayer
	msgbox(ViridianCity_Text_WantToKnowAboutCaterpillarMons, MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, YES, ViridianCity_EventScript_YoungsterExplainCaterpillars)
	goto_if_eq(VAR_RESULT, NO, ViridianCity_EventScript_YoungsterDeclineExplanation)
	end
}

script ViridianCity_EventScript_YoungsterExplainCaterpillars {
	msgbox(ViridianCity_Text_ExplainCaterpieWeedle)
	release
	end
}

script ViridianCity_EventScript_YoungsterDeclineExplanation {
	msgbox(ViridianCity_Text_OhOkayThen)
	release
	end
}

script ViridianCity_EventScript_Woman {
	lock
	faceplayer
	goto_if_eq(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 0, ViridianCity_EventScript_WomanRoadBlocked)
	msgbox(ViridianCity_Text_GoShoppingInPewterOccasionally)
	release
	end
}

script ViridianCity_EventScript_WomanRoadBlocked {
	msgbox(ViridianCity_Text_GrandpaHasntHadCoffeeYet)
	closemessage
	applymovement(LOCALID_WOMAN, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
	end
}

script ViridianCity_EventScript_DreamEaterTutor {
	goto(EventScript_DreamEaterTutor)
	end
}

script ViridianCity_EventScript_RoadBlocked {
	lockall
	msgbox(ViridianCity_Text_ThisIsPrivateProperty)
	closemessage
	applymovement(OBJ_EVENT_ID_PLAYER, ViridianCity_Movement_WalkDown)
	waitmovement(0)
	releaseall
	end
}

script ViridianCity_Movement_WalkDown {
	walk_down
	step_end
}

script ViridianCity_EventScript_TutorialTriggerLeft {
	lockall
	applymovement(LOCALID_TUTORIAL_MAN, Common_Movement_WalkInPlaceFasterLeft)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterRight)
	waitmovement(0)
	call(ViridianCity_EventScript_DoTutorialBattle)
	release
	end
}

script ViridianCity_EventScript_TutorialTriggerRight {
	lockall
	applymovement(LOCALID_TUTORIAL_MAN, Common_Movement_WalkInPlaceFasterRight)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft)
	waitmovement(0)
	call(ViridianCity_EventScript_DoTutorialBattle)
	release
	end
}

script ViridianCity_EventScript_DoTutorialBattle {
	msgbox(ViridianCity_Text_ShowYouHowToCatchMons)
	closemessage
	// TODO - handle new tutorial
	special(SavePlayerParty)
	special(LoadWallyZigzagoon)
	special(StartWallyTutorialBattle)
	special(LoadPlayerParty)
	waitstate
	lock
	faceplayer
	msgbox(ViridianCity_Text_ThatWasEducationalTakeThis)
	setvar(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 2)
	giveitem(ITEM_TEACHY_TV)
	msgbox(ViridianCity_Text_WatchThatToLearnBasics)
	return
}

text ViridianCity_Text_CanCarryMonsAnywhere {
    "Those POKé BALLS at your waist!\n"
    "You have POKéMON, don't you?\p"
    "It's great that you can carry and\n"
    "use POKéMON anytime, anywhere.$"
}

text ViridianCity_Text_GymClosedWonderWhoLeaderIs {
    "This POKéMON GYM is always closed.\p"
    "I wonder who the LEADER is?$"
}

text ViridianCity_Text_ViridiansGymLeaderReturned {
    "VIRIDIAN GYM's LEADER returned!$"
}

text ViridianCity_Text_WantToKnowAboutCaterpillarMons {
    "You want to know about the two\n"
    "kinds of caterpillar POKéMON?$"
}

text ViridianCity_Text_OhOkayThen {
    "Oh, okay then!$"
}

text ViridianCity_Text_ExplainCaterpieWeedle {
    "CATERPIE has no poison,\n"
    "but WEEDLE does.\p"
    "Watch that your POKéMON aren't\n"
    "stabbed by WEEDLE's POISON STING.$"
}

text ViridianCity_Text_GrandpaHasntHadCoffeeYet {
    "Oh, Grandpa!\n"
    "Don't be so mean!\p"
    "I'm so sorry.\n"
    "He hasn't had his coffee yet.$"
}

text ViridianCity_Text_GoShoppingInPewterOccasionally {
    "I go shopping in PEWTER CITY\n"
    "occasionally.\p"
    "I have to take the winding trail in\n"
    "VIRIDIAN FOREST when I go.$"
}

text ViridianCity_Text_ThisIsPrivateProperty {
    "I absolutely forbid you from\n"
    "going through here!\p"
    "This is private property!$"
}

text ViridianCity_Text_ShowYouHowToCatchMons {
    "Well, now, I've had my coffee, and\n"
    "that's what I need to get going!\p"
    "Hm?\n"
    "What is that red box you have?\p"
    "Ah, so you're working on your\n"
    "POKéDEX.\p"
    "Then let me give you a word of\n"
    "advice.\p"
    "Whenever you catch a POKéMON,\n"
    "the POKéDEX automatically updates\l"
    "its data.\p"
    "…You don't know how to catch\n"
    "a POKéMON?\p"
    "I suppose I had better show you\n"
    "then!$"
}

text ViridianCity_Text_ThatWasEducationalTakeThis {
    "There! Now tell me, that was\n"
    "educational, was it not?\p"
    "And here, take this, too.$"
}

text ViridianCity_Text_WatchThatToLearnBasics {
    "If there's something you don't\n"
    "understand, watch that.\p"
    "It will teach you about the basics\n"
    "of being a POKéMON TRAINER.$"
}

text ViridianCity_Text_WeakenMonsFirstToCatch {
    "Well, now, I've had my coffee, and\n"
    "that's what I need to get going!\p"
    "But I made it too strong.\n"
    "It gave me a headache…\p"
    "Incidentally, are you filling your\n"
    "POKéDEX?\p"
    "At first, focus on weakening the\n"
    "POKéMON before trying to catch it.$"
}

text ViridianCity_Text_HowsTeachyTVHelping {
    "Well, now, I've had my coffee, and\n"
    "that's what I need to get going!\p"
    "But I made it too strong.\n"
    "It gave me a headache…\p"
    "Incidentally, is my old TEACHY TV\n"
    "helping you?$"
}

text ViridianCity_Text_MyGrandsonOnTheShow {
    "Wahaha!\n"
    "It's my grandson on the show!\p"
    "Since he's the one doing the\n"
    "teaching, you're bound to learn.$"
}

text ViridianCity_Text_TooBusyForTeachyTV {
    "Hm… You're too busy to not even\n"
    "have the time for TEACHY TV…\p"
    "Time is money, and neither should\n"
    "be ill spent…$"
}

text ViridianCity_Text_CitySign {
    "VIRIDIAN CITY \n"
    "The Eternally Green Paradise$"
}

text ViridianCity_Text_CatchMonsForEasierBattles {
    "TRAINER TIPS\p"
    "Catch POKéMON and expand your\n"
    "collection.\p"
    "The more you have, the easier it\n"
    "is to battle.$"
}

text ViridianCity_Text_MovesLimitedByPP {
    "TRAINER TIPS\p"
    "The battle moves of POKéMON are\n"
    "limited by their POWER POINTS, PP.\p"
    "To replenish PP, rest your tired\n"
    "POKéMON at a POKéMON CENTER.$"
}

text ViridianCity_Text_GymSign {
    "VIRIDIAN CITY POKéMON GYM$"
}

text ViridianCity_Text_GymDoorsAreLocked {
    "VIRIDIAN GYM's doors are locked…$"
}
