const LOCALID_WOMAN = 1
const LOCALID_LASS = 2

mapscripts ViridianCity_School_MapScripts {}

script ViridianCity_School_EventScript_Lass {
	lock
	faceplayer
	msgbox(ViridianCity_School_Text_TryingToMemorizeNotes)
	closemessage
	applymovement(LOCALID_LASS, Common_Movement_WalkInPlaceFasterUp)
	waitmovement(0)
	release
	end
}

script ViridianCity_School_EventScript_Woman {
	lock
	faceplayer
	msgbox(ViridianCity_School_Text_ReadBlackboardCarefully)
	closemessage
	applymovement(LOCALID_WOMAN, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
	end
}

script ViridianCity_School_EventScript_Notebook {
	lockall
	msgbox(ViridianCity_School_Text_NotebookFirstPage)
	msgbox(ViridianCity_School_Text_TurnThePage, MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, NO, ViridianCity_School_EventScript_StopReadingNotebook)
	msgbox(ViridianCity_School_Text_NotebookSecondPage)
	msgbox(ViridianCity_School_Text_TurnThePage, MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, NO, ViridianCity_School_EventScript_StopReadingNotebook)
	msgbox(ViridianCity_School_Text_NotebookThirdPage)
	msgbox(ViridianCity_School_Text_TurnThePage, MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, NO, ViridianCity_School_EventScript_StopReadingNotebook)
	msgbox(ViridianCity_School_Text_NotebookFourthPage)
	applymovement(LOCALID_LASS, Common_Movement_WalkInPlaceFasterUp)
	waitmovement(0)
	msgbox(ViridianCity_School_Text_HeyDontLookAtMyNotes)
	releaseall
	end
}

script ViridianCity_School_EventScript_StopReadingNotebook {
	releaseall
	end
}

script ViridianCity_School_EventScript_Blackboard {
	lockall
	setvar(VAR_TEMP_A, 0) // Tracks previous selection in multichoice
	msgbox(ViridianCity_School_Text_BlackboardListsStatusProblems)
	goto(ViridianCity_School_EventScript_ChooseBlackboardTopic)
	end
}

script ViridianCity_School_EventScript_ChooseBlackboardTopic {
	message(ViridianCity_School_Text_ReadWhichTopic)
	waitmessage
	dynmultichoice(10, 0, FALSE, 6, VAR_TEMP_A, DYN_MULTICHOICE_CB_NONE, "Sleep", "Poison", "Paralysis", "Burn", "Freeze", "Exit")
	switch (var(VAR_RESULT)) {
	    case 0: goto(ViridianCity_School_EventScript_ReadSleep)
	    case 1: goto(ViridianCity_School_EventScript_ReadPoison)
	    case 2: goto(ViridianCity_School_EventScript_ReadParalysis)
	    case 3: goto(ViridianCity_School_EventScript_ReadBurn)
	    case 4: goto(ViridianCity_School_EventScript_ReadFreeze)
	    case 5: goto(ViridianCity_School_EventScript_ExitBlackboard)
	    case 127: goto(ViridianCity_School_EventScript_ExitBlackboard)
    }
	end
}

script ViridianCity_School_EventScript_ReadSleep {
	setvar(VAR_TEMP_A, 0)
	msgbox(ViridianCity_School_Text_ExplainSleep)
	goto(ViridianCity_School_EventScript_ChooseBlackboardTopic)
	end
}

script ViridianCity_School_EventScript_ReadPoison {
	setvar(VAR_TEMP_A, 1)
	msgbox(ViridianCity_School_Text_ExplainPoison)
	goto(ViridianCity_School_EventScript_ChooseBlackboardTopic)
	end
}

script ViridianCity_School_EventScript_ReadParalysis {
	setvar(VAR_TEMP_A, 2)
	msgbox(ViridianCity_School_Text_ExplainParalysis)
	goto(ViridianCity_School_EventScript_ChooseBlackboardTopic)
	end
}

script ViridianCity_School_EventScript_ReadBurn {
	setvar(VAR_TEMP_A, 3)
	msgbox(ViridianCity_School_Text_ExplainBurn)
	goto(ViridianCity_School_EventScript_ChooseBlackboardTopic)
	end
}

script ViridianCity_School_EventScript_ReadFreeze {
	setvar(VAR_TEMP_A, 4)
	msgbox(ViridianCity_School_Text_ExplainFreeze)
	goto(ViridianCity_School_EventScript_ChooseBlackboardTopic)
	end
}

script ViridianCity_School_EventScript_ExitBlackboard {
	releaseall
	end
}

script ViridianCity_School_EventScript_PokemonJournal {
	lock
	faceplayer
	msgbox(PokemonJournal_Text_SpecialFeatureProfOak)
	release
	end
}

text ViridianCity_School_Text_TryingToMemorizeNotes {
    "Whew! I'm trying to memorize all my\n"
    "notes.$"
}

text ViridianCity_School_Text_ReadBlackboardCarefully {
    "Okay!\p"
    "Be sure to read what's on the\n"
    "blackboard carefully!$"
}

text ViridianCity_School_Text_NotebookFirstPage {
    "Let's check out the notebook.\p"
    "First page…\p"
    "POKé BALLS are used to catch\n"
    "POKéMON.\p"
    "Up to six POKéMON can be carried\n"
    "in your party.\p"
    "People who raise and battle\n"
    "with POKéMON are called TRAINERS.$"
}

text ViridianCity_School_Text_NotebookSecondPage {
    "Second page…\p"
    "A healthy POKéMON may be hard to\n"
    "catch, so weaken it first.\p"
    "Poison, burn, or cause another\n"
    "status problem to weaken it.$"
}

text ViridianCity_School_Text_NotebookThirdPage {
    "Third page…\p"
    "POKéMON TRAINERS seek others to\n"
    "engage in POKéMON battles.\p"
    "To TRAINERS, the taste of victory\n"
    "is sweet, indeed.\p"
    "Battles are constantly waged at\n"
    "POKéMON GYMS everywhere.$"
}

text ViridianCity_School_Text_NotebookFourthPage {
    "Fourth page…\p"
    "The ultimate goal for all POKéMON\n"
    "TRAINERS is simple.\p"
    "It is to defeat the top eight\n"
    "POKéMON GYM LEADERS.\p"
    "Do so to earn the right to face…\p"
    "The ELITE FOUR of the POKéMON\n"
    "LEAGUE!$"
}

text ViridianCity_School_Text_TurnThePage {
    "Turn the page?$"
}

text ViridianCity_School_Text_HeyDontLookAtMyNotes {
    "GIRL: Hey!\n"
    "Don't look at my notes!$"
}

text ViridianCity_School_Text_BlackboardListsStatusProblems {
    "The blackboard lists POKéMON\n"
    "STATUS problems during battles.$"
}

text ViridianCity_School_Text_ReadWhichTopic {
    "Which topic do you want to read?$"
}

text ViridianCity_School_Text_ExplainSleep {
    "A POKéMON can't attack if it's\n"
    "asleep.\p"
    "POKéMON will stay asleep even\n"
    "after battles.\p"
    "Use AWAKENING to awaken one\n"
    "from sleep.$"
}

text ViridianCity_School_Text_ExplainBurn {
    "A burn reduces ATTACK power.\n"
    "It also causes ongoing HP loss.\p"
    "A burn remains after a battle.\n"
    "Use BURN HEAL to cure a burn.$"
}

text ViridianCity_School_Text_ExplainPoison {
    "When poisoned, a POKéMON's health\n"
    "steadily drops.\p"
    "Poison lingers after battles.\n"
    "Use an ANTIDOTE to cure poison!$"
}

text ViridianCity_School_Text_ExplainFreeze {
    "A frozen POKéMON becomes\n"
    "helplessly immobile.\p"
    "It stays frozen even after the\n"
    "battle ends.\p"
    "Use ICE HEAL to thaw out the\n"
    "suffering POKéMON.$"
}

text ViridianCity_School_Text_ExplainParalysis {
    "Paralysis reduces SPEED and may\n"
    "prevent the POKéMON from moving.\p"
    "Paralysis remains after battles.\n"
    "Use PARLYZ HEAL for treatment.$"
}

text PokemonJournal_Text_SpecialFeatureProfOak {
	"POKéMON JOURNAL\p"
	"Special Feature: PROF. OAK,\n"
	"the POKéMON Researcher!\p"
	"PROF. OAK reportedly lives with his\n"
	"grandchildren DAISY and {RIVAL}.$"
}
