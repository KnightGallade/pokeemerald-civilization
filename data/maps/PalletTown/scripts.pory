const LOCALID_SIGN_LADY = 1
const LOCALID_PROF_OAK =  3
const LOCALID_CELEBI = 4
const SIGN_LADY_READY = VAR_TEMP_2

mapscripts PalletTown_MapScripts {
	MAP_SCRIPT_ON_TRANSITION: PalletTown_OnTransition
	MAP_SCRIPT_ON_FRAME_TABLE [VAR_MAP_SCENE_PALLET_TOWN_OAK, 2: PalletTown_EventScript_OakRatingScene]
}

script PalletTown_OnTransition {
	setflag(FLAG_VISITED_PALLET_TOWN)
	call_if_set(FLAG_PALLET_LADY_NOT_BLOCKING_SIGN, PalletTown_EventScript_TryReadySignLady)
	call_if_eq(VAR_MAP_SCENE_PALLET_TOWN_SIGN_LADY, 0, PalletTown_EventScript_SetSignLadyPos)
	call_if_eq(VAR_MAP_SCENE_PALLET_TOWN_SIGN_LADY, 1, PalletTown_EventScript_SetSignLadyDone)
	end
}

script PalletTown_EventScript_TryReadySignLady {
	goto_if_unset(FLAG_TEMP_A, PalletTown_EventScript_ReusableReturn)
	goto_if_ge(VAR_MAP_SCENE_PALLET_TOWN_SIGN_LADY, 1, PalletTown_EventScript_ReusableReturn)
	setvar(VAR_MAP_SCENE_PALLET_TOWN_SIGN_LADY, 1)
	return
}

script PalletTown_EventScript_ReusableReturn {
	return
}

script PalletTown_EventScript_SetSignLadyDone {
	setvar(VAR_MAP_SCENE_PALLET_TOWN_SIGN_LADY, 2)
	return
}

script PalletTown_EventScript_SetSignLadyPos {
	goto_if_set(FLAG_PALLET_LADY_NOT_BLOCKING_SIGN, PalletTown_EventScript_MoveSignLadyToRouteEntrance)
	setobjectxyperm(LOCALID_SIGN_LADY, 5, 15)
	setobjectmovementtype(LOCALID_SIGN_LADY, MOVEMENT_TYPE_FACE_UP)
	return
}

script PalletTown_EventScript_MoveSignLadyToRouteEntrance {
	setobjectxyperm(LOCALID_SIGN_LADY, 12, 2)
	setobjectmovementtype(LOCALID_SIGN_LADY, MOVEMENT_TYPE_FACE_DOWN)
	setvar(SIGN_LADY_READY, TRUE)
	return
}

// Oak approaches player after exiting their house post Elite Four to check if they should receieve the National Dex
script PalletTown_EventScript_OakRatingScene {
	lockall
	setobjectxyperm(LOCALID_PROF_OAK, 14, 14)
	setobjectmovementtype(LOCALID_PROF_OAK, MOVEMENT_TYPE_FACE_LEFT)
	addobject(LOCALID_PROF_OAK)
	applymovement(LOCALID_PROF_OAK, PalletTown_Movement_OakWalkToPlayersDoor)
	waitmovement(0)
	// TODO - handle the whole Sevii Island thing
	// Original line: goto_if_unset(FLAG_WORLD_MAP_ONE_ISLAND, PalletTown_EventScript_NotBeenToOneIslandYet)
	goto(PalletTown_EventScript_NotBeenToOneIslandYet)
	msgbox(PalletTown_Text_OakLetMeSeePokedex)
	setvar(VAR_0x8004, 0)
	specialvar(VAR_RESULT, ScriptGetPokedexInfo)
	copyvar(VAR_0x8008, VAR_0x8005)
	copyvar(VAR_0x8009, VAR_0x8006)
	buffernumberstring(STR_VAR_1, VAR_0x8008)
	buffernumberstring(STR_VAR_2, VAR_0x8009)
	goto_if_lt(VAR_0x8009, 60, PalletTown_EventScript_NotEnoughMonsForNationalDex)
	msgbox(PalletTown_Text_CaughtXImpressiveFollowMe)
	closemessage
	playbgm(MUS_FOLLOW_ME, 0)
	applymovement(OBJ_EVENT_ID_PLAYER, PalletTown_Movement_PlayerWalkToLabFromHouse)
	applymovement(LOCALID_PROF_OAK, PalletTown_Movement_OakWalkToLabFromHouse)
	waitmovement(0)
	opendoor(17, 13)
	waitdooranim
	applymovement(LOCALID_PROF_OAK, PalletTown_Movement_OakEnterLab)
	applymovement(OBJ_EVENT_ID_PLAYER, PalletTown_Movement_PlayerEnterLab)
	waitmovement(0)
	closedoor(17, 13)
	waitdooranim
	setvar(VAR_MAP_SCENE_PALLET_TOWN_PROFESSOR_OAKS_LAB, 7)
	setvar(VAR_MAP_SCENE_PALLET_TOWN_OAK, 3)
	setflag(FLAG_HIDE_OAK_IN_PALLET_TOWN)
	warp(MAP_PALLET_TOWN_PROFESSOR_OAKS_LAB, 6, 12)
	waitstate
	releaseall
	end
}

script PalletTown_EventScript_EndOakRatingScene {
	closemessage
	applymovement(LOCALID_PROF_OAK, PalletTown_Movement_OakExit)
	waitmovement(0)
	removeobject(LOCALID_PROF_OAK)
	setvar(VAR_MAP_SCENE_PALLET_TOWN_OAK, 3)
	releaseall
	end
}

script PalletTown_EventScript_NotEnoughMonsForNationalDex {
	msgbox(PalletTown_Text_CaughtXPuttingInHonestEffort)
	goto(PalletTown_EventScript_EndOakRatingScene)
	end
}

script PalletTown_EventScript_NotBeenToOneIslandYet {
	msgbox(PalletTown_Text_OakYouEnjoyingTraveling)
	goto(PalletTown_EventScript_EndOakRatingScene)
	end
}

movement PalletTown_Movement_OakWalkToPlayersDoor {
	walk_left
	walk_left
	walk_left
	walk_up
	walk_up
	walk_up
	walk_up
	walk_up
	walk_left
	walk_left
	walk_left
	walk_left
	walk_left
	walk_in_place_faster_up
	step_end
}

movement PalletTown_Movement_OakExit {
	walk_right
	walk_right
	walk_right
	walk_right
	walk_right
	walk_down
	walk_down
	walk_down
	walk_down
	walk_down
	walk_right
	walk_right
	walk_right
	step_end
}

movement PalletTown_Movement_OakWalkToLabFromHouse {
	walk_right
	walk_right
	walk_right
	walk_right
	walk_right
	walk_down
	walk_down
	walk_down
	walk_down
	walk_down
	walk_right
	walk_right
	walk_right
	walk_right
	walk_right
	walk_in_place_faster_up
	step_end
}

movement PalletTown_Movement_PlayerWalkToLabFromHouse {
	walk_down
	walk_right
	walk_right
	walk_right
	walk_right
	walk_right
	walk_down
	walk_down
	walk_down
	walk_down
	walk_down
	walk_right
	walk_right
	walk_right
	walk_right
	step_end
}

script PalletTown_EventScript_OakTriggerLeft {
	lockall
	setvar(VAR_TEMP_1, 0)
	goto(PalletTown_EventScript_OakTrigger)
	end
}

script PalletTown_EventScript_OakTriggerRight {
	lockall
	setvar(VAR_TEMP_1, 1)
	goto(PalletTown_EventScript_OakTrigger)
	end
}

script PalletTown_EventScript_OakTrigger {
	delay(30)
	playbgm(MUS_RG_OAK, 0)
	message(PalletTown_Text_OakDontGoOut)
	waitmessage
	delay(85)
	closemessage
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterDown)
	waitmovement(0)
	playse(SE_PIN)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	waitmovement(0)
	delay(30)
	addobject(LOCALID_PROF_OAK)
	call_if_eq(VAR_TEMP_1, 0, PalletTown_EventScript_OakEnterLeft)
	call_if_eq(VAR_TEMP_1, 1, PalletTown_EventScript_OakEnterRight)
	delay(30)
	msgbox(PalletTown_Text_OakGrassUnsafeNeedMon)
	closemessage
	delay(30)
	call_if_eq(VAR_TEMP_1, 0, PalletTown_EventScript_OakLeadPlayerToLabLeft)
	call_if_eq(VAR_TEMP_1, 1, PalletTown_EventScript_OakLeadPlayerToLabRight)
	opendoor(17, 13)
	waitdooranim
	applymovement(LOCALID_PROF_OAK, PalletTown_Movement_OakEnterLab)
	applymovement(OBJ_EVENT_ID_PLAYER, PalletTown_Movement_PlayerEnterLab)
	waitmovement(0)
	closedoor(17, 13)
	waitdooranim
	setvar(VAR_MAP_SCENE_PALLET_TOWN_PROFESSOR_OAKS_LAB, 1)
	clearflag(FLAG_HIDE_OAK_IN_HIS_LAB)
	setvar(VAR_MAP_SCENE_PALLET_TOWN_OAK, 1)
	setflag(FLAG_HIDE_OAK_IN_PALLET_TOWN)
	setflag(FLAG_DONT_TRANSITION_MUSIC)
	warp(MAP_PALLET_TOWN_PROFESSOR_OAKS_LAB, 6, 12)
	waitstate
	releaseall
	end
}

script PalletTown_EventScript_OakEnterLeft {
	applymovement(LOCALID_PROF_OAK, PalletTown_Movement_OakEnterLeft)
	waitmovement(0)
	return
}

script PalletTown_EventScript_OakEnterRight {
	applymovement(LOCALID_PROF_OAK, PalletTown_Movement_OakEnterRight)
	waitmovement(0)
	return
}

script PalletTown_EventScript_OakLeadPlayerToLabLeft {
	applymovement(LOCALID_PROF_OAK, PalletTown_Movement_OakWalkToLabLeft)
	applymovement(OBJ_EVENT_ID_PLAYER, PalletTown_Movement_PlayerWalkToLabLeft)
	waitmovement(0)
	return
}

script PalletTown_EventScript_OakLeadPlayerToLabRight {
	applymovement(LOCALID_PROF_OAK, PalletTown_Movement_OakWalkToLabRight)
	applymovement(OBJ_EVENT_ID_PLAYER, PalletTown_Movement_PlayerWalkToLabRight)
	waitmovement(0)
	return
}
 movement PalletTown_Movement_OakEnterLeft {
	walk_up
	walk_up
	walk_right
	walk_up
	walk_up
	walk_right
	walk_up
	walk_up
	step_end
 }

 movement PalletTown_Movement_OakEnterRight {
	walk_right
	walk_up
	walk_up
	walk_right
	walk_up
	walk_up
	walk_right
	walk_up
	walk_up
	step_end
 }

raw `
	.macro walk_to_lab
	walk_left
	walk_down
	walk_down
	walk_down
	walk_down
	walk_down
	walk_down
	walk_down
	walk_down
	walk_down
	walk_down
	walk_down
	walk_right
	walk_right
	walk_right
	walk_right
	walk_right
	.endm
`

movement PalletTown_Movement_OakWalkToLabLeft {
	walk_down
	walk_to_lab
	walk_right
	walk_in_place_faster_up
	step_end
}

movement PalletTown_Movement_OakWalkToLabRight {
	walk_down
	walk_left
	walk_to_lab
	walk_right
	walk_in_place_faster_up
	step_end
}

movement PalletTown_Movement_OakEnterLab {
	walk_up
	set_invisible
	step_end
}

movement PalletTown_Movement_PlayerWalkToLabLeft {
	walk_down
	walk_down
	walk_to_lab
	step_end
}

movement PalletTown_Movement_PlayerWalkToLabRight {
	walk_down
	walk_down
	walk_left
	walk_to_lab
	step_end
}

movement PalletTown_Movement_PlayerEnterLab {
	walk_right
	walk_up
	set_invisible
	step_end
}

script PalletTown_EventScript_SignLady {
	lock
	goto_if_eq(VAR_MAP_SCENE_PALLET_TOWN_SIGN_LADY, 2, PalletTown_EventScript_SignLadyDone)
	goto_if_eq(VAR_MAP_SCENE_PALLET_TOWN_SIGN_LADY, 1, PalletTown_EventScript_SignLadyJustShowedSign)
	goto_if_eq(SIGN_LADY_READY, TRUE, PalletTown_EventScript_SignLadyStartShowSign)
	goto_if_set(FLAG_TEMP_2, PalletTown_EventScript_SignLadyGoReadSign)
	msgbox(PalletTown_Text_HmmIsThatRight)
	applymovement(LOCALID_SIGN_LADY, Common_Movement_FacePlayer)
	waitmovement(0)
	playse(SE_PIN)
	applymovement(LOCALID_SIGN_LADY, Common_Movement_ExclamationMark)
	waitmovement(0)
	applymovement(LOCALID_SIGN_LADY, Common_Movement_Delay48)
	waitmovement(0)
	msgbox(PalletTown_Text_OhLookLook)
	closemessage
	call_if_eq(VAR_FACING, DIR_EAST, PalletTown_EventScript_SignLadyMoveOutOfWayRight)
	call_if_ne(VAR_FACING, DIR_EAST, PalletTown_EventScript_SignLadyMoveOutOfWayLeft)
	copyobjectxytoperm(LOCALID_SIGN_LADY)
	setflag(FLAG_TEMP_2)
	release
	end
}

script PalletTown_EventScript_SignLadyMoveOutOfWayRight {
	applymovement(LOCALID_SIGN_LADY, PalletTown_Movement_SignLadyMoveOutOfWayRight)
	waitmovement(0)
	return
}

script PalletTown_EventScript_SignLadyMoveOutOfWayLeft {
	applymovement(LOCALID_SIGN_LADY, PalletTown_Movement_SignLadyMoveOutOfWayLeft)
	waitmovement(0)
	return
}

script PalletTown_EventScript_SignLadyDone {
	applymovement(LOCALID_SIGN_LADY, Common_Movement_FacePlayer)
	waitmovement(0)
	msgbox(PalletTown_Text_RaisingMonsToo)
	release
	end
}

script PalletTown_EventScript_SignLadyGoReadSign {
	applymovement(LOCALID_SIGN_LADY, Common_Movement_FacePlayer)
	waitmovement(0)
	msgbox(PalletTown_Text_ReadItReadIt)
	release
	end
}

script PalletTown_EventScript_SignLadyJustShowedSign {
	applymovement(LOCALID_SIGN_LADY, Common_Movement_FacePlayer)
	waitmovement(0)
	msgbox(PalletTown_Text_SignsAreUsefulArentThey)
	release
	end
}

movement PalletTown_Movement_SignLadyMoveOutOfWayRight {
	walk_right
	walk_in_place_faster_left
	step_end
}

movement PalletTown_Movement_SignLadyMoveOutOfWayLeft {
	walk_left
	walk_in_place_faster_right
	step_end
}

script PalletTown_EventScript_FatMan {
	msgbox(PalletTown_Text_CanStoreItemsAndMonsInPC, MSGBOX_NPC)
	end
}

script PalletTown_EventScript_OaksLabSign {
	lockall
	msgbox(PalletTown_Text_OakPokemonResearchLab)
	releaseall
	end
}

script PalletTown_EventScript_PlayersHouseSign {
	msgbox(PalletTown_Text_PlayersHouse, MSGBOX_SIGN)
	end
}

script PalletTown_EventScript_RivalsHouseSign {
	msgbox(PalletTown_Text_RivalsHouse, MSGBOX_SIGN)
	end
}

script PalletTown_EventScript_TownSign {
	msgbox(PalletTown_Text_TownSign, MSGBOX_SIGN)
	end
}

script PalletTown_EventScript_TrainerTips{
	lockall
	msgbox(PalletTown_Text_PressStartToOpenMenu)
	setvar(VAR_MAP_SCENE_PALLET_TOWN_SIGN_LADY, 1)
	releaseall
	end
}

script PalletTown_EventScript_SignLadyTrigger {
	lockall
	applymovement(LOCALID_SIGN_LADY, Common_Movement_WalkInPlaceFasterRight)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft)
	waitmovement(0)
	call(PalletTown_EventScript_SignLadyShowSign)
	releaseall
	end
}

script PalletTown_EventScript_SignLadyShowSign {
	msgbox(PalletTown_Text_LookCopiedTrainerTipsSign)
	closemessage
	delay(20)
	setflag(FLAG_TEMP_A)
	setvar(VAR_MAP_SCENE_PALLET_TOWN_SIGN_LADY, 1)
	setvar(SIGN_LADY_READY, FALSE)
	// TODO - figure out this line
	// special(SetWalkingIntoSignVars)
	// special(DisableMsgBoxWalkaway)
	signmsg
	msgbox(PalletTown_Text_PressStartToOpenMenuCopy)
	normalmsg
	return
}

script PalletTown_EventScript_SignLadyStartShowSign {
	applymovement(LOCALID_SIGN_LADY, Common_Movement_FacePlayer)
	waitmovement(0)
	call(PalletTown_EventScript_SignLadyShowSign)
	release
	end
}

script PalletTown_EventScript_Talk_To_Celebi {
	lockall
	applymovement(LOCALID_CELEBI, moves(face_player, emote_exclamation_mark))
	msgbox(PalletTown_EventScript_Ask_Celebi, MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, YES, PalletTown_EventScript_Celebi_Back_In_Time)
	msgbox(PalletTown_EventScript_Refuse_Celebi, MSGBOX_NPC)
	releaseall
	end
}

script PalletTown_EventScript_Celebi_Back_In_Time {
	fadenewbgm(MUS_CAVE_OF_ORIGIN)
	msgbox(LittlerootTown_EventScript_Accepted_Celebi, MSGBOX_NPC)
	setvar(VAR_RESULT, 2)
	special(DoOrbEffect)
	lockall
	delay(150)
	playmoncry(SPECIES_CELEBI, CRY_MODE_ENCOUNTER)
	special(FadeOutOrbEffect)
	lockall
	delay(30)
	waitmoncry
	playse(SE_SHINY)
	checkplayergender
	goto_if_eq(VAR_RESULT, MALE, PalletTown_EventScript_Celebi_Male)
	goto_if_eq(VAR_RESULT, FEMALE, PalletTown_EventScript_Celebi_Female)
}

script PalletTown_EventScript_Celebi_Male {
	setrespawn(HEAL_LOCATION_LITTLEROOT_TOWN_BRENDANS_HOUSE_2F)
	warpteleport(MAP_LITTLEROOT_TOWN_BRENDANS_HOUSE_2F, 1, 4)
	releaseall
	end
}

script PalletTown_EventScript_Celebi_Female {
	setrespawn(HEAL_LOCATION_LITTLEROOT_TOWN_MAYS_HOUSE_2F)
	warpteleport(MAP_LITTLEROOT_TOWN_MAYS_HOUSE_2F, 7, 4)
	releaseall
	end
}

text PalletTown_EventScript_Ask_Celebi {
	"It appears Celebi wants you to \n"
	"follow it...\l"
	"Do you wish to go with Celebi?$"
}

text PalletTown_EventScript_Refuse_Celebi {
	"Yeah, FireRed is pretty cool.$"
}

text PalletTown_EventScript_Accepted_Celebi {
	"You enter a different reality...$"
}

// Original text
text PalletTown_Text_OakDontGoOut {
	"OAK: Hey! Wait!\n"
	"Don't go out!$"
}

text PalletTown_Text_OakGrassUnsafeNeedMon {
	"OAK: It's unsafe!\n"
	"Wild POKéMON live in tall grass!\p"
	"You need your own POKéMON for\n"
	"your protection.\p"
	"I know!\n"
	"Here, come with me!$"
}

text PalletTown_Text_RaisingMonsToo {
	"I'm raising POKéMON, too.\p"
	"When they get strong, they can\n"
	"protect me.$"
}

text PalletTown_Text_CanStoreItemsAndMonsInPC {
	"Technology is incredible!\p"
	"You can now store and recall items\n"
	"and POKéMON as data via PC.$"
}

text PalletTown_Text_OakPokemonResearchLab {
	"OAK POKéMON RESEARCH LAB$"
}

text PalletTown_Text_PlayersHouse {
	"{PLAYER}'s house$"
}

text PalletTown_Text_RivalsHouse {
	"{RIVAL}'s house$"
}

text PalletTown_Text_TownSign {
	"PALLET TOWN\n"
	"Shades of your journey await!$"
}

text PalletTown_Text_OakLetMeSeePokedex {
	"OAK: Ah, {PLAYER}!\n"
	"You're back, are you?\p"
	"How much have you filled in your\n"
	"POKéDEX?\p"
	"May I see it?\p"
	"Let's see…$"
}

text  PalletTown_Text_CaughtXPuttingInHonestEffort {
	"You've caught {STR_VAR_2}…\p"
	"Hm, it looks as if you're putting\n"
	"in an honest effort.\p"
	"When you manage to fill it some\n"
	"more, come show me, please.$"
}

text PalletTown_Text_CaughtXImpressiveFollowMe {
	"You've caught… {STR_VAR_2}!?\n"
	"Now, this is impressive!\p"
	"There's something I wanted to ask\n"
	"of you, {PLAYER}.\p"
	"Come.\n"
	"Follow me.$"
}

text PalletTown_Text_OakYouEnjoyingTraveling {
	"OAK: Ah, {PLAYER}!\n"
	"You seem to be enjoying traveling.\p"
	"Knowing you, {PLAYER}, I can easily\n"
	"imagine you going out to even more\l"
	"exotic locales.\p"
	"Good for you, good for you.\n"
	"Hohoho.$"
}

// For the NPC in Pallet Town that talks about the Trainer Tips sign

text PalletTown_Text_HmmIsThatRight {
	"Hmm…\n"
	"Is that right…$"
}

text PalletTown_Text_OhLookLook {
	"Oh!\n"
	"Look, look!$"
}

text PalletTown_Text_ReadItReadIt {
	"Read it, read it!$"
}

// Used by sign
text PalletTown_Text_PressStartToOpenMenu {
	"TRAINER TIPS\p"
	"Press START to open the MENU!$"
}

text PalletTown_Text_SignsAreUsefulArentThey {
	"Signs are useful, aren't they?$"
}

text PalletTown_Text_LookCopiedTrainerTipsSign {
	"Look, look!\p"
	"I copied what it said on one of\n"
	"those TRAINER TIPS signs!$"
}

// Used by NPC
text PalletTown_Text_PressStartToOpenMenuCopy {
	"TRAINER TIPS!\p"
	"Press START to open the MENU!$"
}
