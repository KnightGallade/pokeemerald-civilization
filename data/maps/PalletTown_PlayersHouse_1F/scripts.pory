const LOCALID_MOM = 1

mapscripts PalletTown_PlayersHouse_1F_MapScripts{}

script PalletTown_PlayersHouse_1F_EventScript_Mom {
	lock
	faceplayer
	goto_if_set(FLAG_BEAT_RIVAL_IN_OAKS_LAB, PalletTown_PlayersHouse_1F_EventScript_MomHeal)
	checkplayergender
	call_if_eq(VAR_RESULT, MALE, PalletTown_PlayersHouse_1F_EventScript_MomOakLookingForYouMale)
	call_if_eq(VAR_RESULT, FEMALE, PalletTown_PlayersHouse_1F_EventScript_MomOakLookingForYouFemale)
	closemessage
	applymovement(LOCALID_MOM, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
	end
}

script PalletTown_PlayersHouse_1F_EventScript_MomOakLookingForYouMale {
	msgbox(PalletTown_PlayersHouse_1F_Text_AllBoysLeaveOakLookingForYou)
	return
}

script PalletTown_PlayersHouse_1F_EventScript_MomOakLookingForYouFemale{
	msgbox(PalletTown_PlayersHouse_1F_Text_AllGirlsLeaveOakLookingForYou)
	return
}

script PalletTown_PlayersHouse_1F_EventScript_MomHeal {
	msgbox(PalletTown_PlayersHouse_1F_Text_YouShouldTakeQuickRest)
	closemessage
	call(Common_EventScript_OutOfCenterPartyHeal)
	msgbox(PalletTown_PlayersHouse_1F_Text_LookingGreatTakeCare)
	release
	end
}

// Displays special text if interacted with from side or back (which are normally inaccessible)
script PalletTown_PlayersHouse_1F_EventScript_TV {
	lockall
	goto_if_eq(VAR_FACING, DIR_NORTH, PalletTown_PlayersHouse_1F_EventScript_TVScreen)
	msgbox(PalletTown_PlayersHouse_1F_Text_OopsWrongSide)
	releaseall
	end
}

script PalletTown_PlayersHouse_1F_EventScript_TVScreen {
	checkplayergender
	call_if_eq(VAR_RESULT, MALE, PalletTown_PlayersHouse_1F_EventScript_TVScreenMale)
	call_if_eq(VAR_RESULT, FEMALE, PalletTown_PlayersHouse_1F_EventScript_TVScreenFemale)
	releaseall
	end
}

script PalletTown_PlayersHouse_1F_EventScript_TVScreenMale {
	msgbox(PalletTown_PlayersHouse_1F_Text_MovieOnTVFourBoysOnRailroad)
	return
}

script PalletTown_PlayersHouse_1F_EventScript_TVScreenFemale {
	msgbox(PalletTown_PlayersHouse_1F_Text_MovieOnTVGirlOnBrickRoad)
	return
}

text PalletTown_PlayersHouse_1F_Text_AllBoysLeaveOakLookingForYou {
	"MOM: …Right.\n"
	"All boys leave home someday.\l"
	"It said so on TV.\p"
	"Oh, yes. PROF. OAK, next door, was\n"
	"looking for you.$"
}

text PalletTown_PlayersHouse_1F_Text_AllGirlsLeaveOakLookingForYou {
	"MOM: …Right.\n"
	"All girls dream of traveling.\l"
	"It said so on TV.\p"
	"Oh, yes. PROF. OAK, next door, was\n"
	"looking for you.$"
}

text PalletTown_PlayersHouse_1F_Text_YouShouldTakeQuickRest {
	"MOM: {PLAYER}!\n"
	"You should take a quick rest.$"
}

text PalletTown_PlayersHouse_1F_Text_LookingGreatTakeCare {
	"MOM: Oh, good! You and your\n"
	"POKéMON are looking great.\l"
	"Take care now!$"
}

text PalletTown_PlayersHouse_1F_Text_MovieOnTVFourBoysOnRailroad {
	"There's a movie on TV.\n"
	"Four boys are walking on railroad\l"
	"tracks.\p"
	"…I better go, too.$"
}


text PalletTown_PlayersHouse_1F_Text_MovieOnTVGirlOnBrickRoad {
	"There's a movie on TV.\n"
	"A girl with her hair in pigtails is\l"
	"walking up a brick road.\p"
	"…I better go, too.$"
}

text PalletTown_PlayersHouse_1F_Text_OopsWrongSide {
	"Oops, wrong side…$"
}

